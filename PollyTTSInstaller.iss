; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Qeslin Player"
#define MyAppVersion "0.2"
#define MyAppPublisher "Sean McNamara"
#define MyAppURL "https://github.com/allquixotic/qeslin-player"
#define DebugOrRelease "Release"
#IFDEF UNICODE
  #DEFINE AW "W"
#ELSE
  #DEFINE AW "A"
#ENDIF

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{A6481E88-FD76-4BE2-BDF9-BB38F8A37B96}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={commonpf}\{#MyAppName}
DefaultGroupName={#MyAppName}                  
OutputBaseFilename=setup
OutputDir=installer
Compression=lzma
SolidCompression=yes            
PrivilegesRequired=admin
LicenseFile=license.txt
WizardStyle=modern   
DisableWelcomePage=yes
ArchitecturesInstallIn64BitMode=x64

[Files]
Source: ".\PrivateData\*"; DestDir: "{%USERPROFILE}"; Flags: skipifsourcedoesntexist ignoreversion confirmoverwrite ignoreversion 64bit; Check: IsWin64
Source: ".\PollyPlayer\bin\{#DebugOrRelease}\QeslinPlayer.exe"; DestDir: "{app}"; Flags: ignoreversion 64bit; Check: IsWin64
Source: ".\redist\vc_redist.x64.exe"; DestDir: {tmp}; Flags: deleteafterinstall
Source: ".\VBCABLE_Driver_Pack43\*"; DestDir: "{app}\VBCABLE"; Flags: recursesubdirs

[Icons]
Name: "{group}\Qeslin Player"; Filename: "{app}\QeslinPlayer.exe"; WorkingDir: "{app}"

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Run]
Filename: "{tmp}\vc_redist.x64.exe"; Parameters: "/install /passive /norestart"; Check: VCRedistNeedsInstall; StatusMsg: Installing Visual Studio Runtime Libraries...
Filename: "{app}\VBCABLE\VBCABLE_Setup_x64.exe"; Flags: runascurrentuser runhidden; Parameters: "-i -h"; StatusMsg: "Installing VB-Cable...";

[UninstallRun]
Filename: "{app}\VBCABLE\VBCABLE_Setup_x64.exe"; Parameters: "-u -h"; Flags: runhidden;

[Code]  
type
  INSTALLSTATE = Longint;

  const
  INSTALLSTATE_INVALIDARG = -2;  { An invalid parameter was passed to the function. }
  INSTALLSTATE_UNKNOWN = -1;     { The product is neither advertised or installed. }
  INSTALLSTATE_ADVERTISED = 1;   { The product is advertised but not installed. }
  INSTALLSTATE_ABSENT = 2;       { The product is installed for a different user. }
  INSTALLSTATE_DEFAULT = 5;      { The product is installed for the current user. }

  { Visual C++ 2017 Redistributable 14.16.27024 }
  VC_2017_REDIST_X84_ADD = '{7258184A-EC44-4B1A-A7D3-68D85A35BFD0}';
  VC_2017_REDIST_X84_MIN = '{5EEFCEFB-E5F7-4C82-99A5-813F04AA4FBD}';

  VC_2017_REDIST_X64_ADD = '{9D29FC96-9EEE-4253-943F-96B3BBFDD0B6}';
  VC_2017_REDIST_X64_MIN = '{F1B0FB3A-E0EA-47A6-9383-3650655403B0}';

function MsiQueryProductState(szProduct: string): INSTALLSTATE; 
  external 'MsiQueryProductState{#AW}@msi.dll stdcall';

function VCVersionInstalled(const ProductID: string): Boolean;
begin
  Result := MsiQueryProductState(ProductID) = INSTALLSTATE_DEFAULT;
end;

function VCRedistNeedsInstall: Boolean;
begin
  Result := not VCVersionInstalled(VC_2017_REDIST_X64_MIN);
end;
